import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

/*
 * Copyright (c) 2018.
 *
 * This file is part of ProcessManager.
 *
 * ProcessManager is free software: you can redistribute it and/or modify it under the terms of version 3 of the
 * GNU Lesser General Public License as published by the Free Software Foundation.
 *
 * ProcessManager is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even
 * the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License along with ProcessManager.  If not,
 * see <http://www.gnu.org/licenses/>.
 */

buildscript {
    ext.androidPluginVersion = '3.1.2'
//    ext.androidPluginVersion = '2.3.3'
    ext.kotlin_version = '1.2.41'
    ext.kotlin_plugin_version = kotlin_version
//    ext.kotlinx_html_version = '0.5.12'
    ext.kotlinx_html_version = '0.6.10'
    ext.codegen_version = '0.5.8'
    ext.junit5_version = '1.2.0'
    ext.jupiterVersion = '5.2.0'
    ext.spekVersion = '1.1.5'
    ext.testngVersion = '6.9.13.8'
    ext.serialization_version = "$serializationVersion"
    ext.argJvmDefault="-Xenable-jvm-default"
//    ext.argJvmDefault="-Xjvm-default=enable" // for 1.2.50
    repositories {
        mavenLocal()
        google()
        jcenter()
        maven { url = "https://dl.bintray.com/kotlin/kotlin-eap" }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$androidPluginVersion" //1.5.0
        classpath 'com.bmuschko:gradle-tomcat-plugin:2.2.4'

        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }

}

//plugins {
//    id "net.devrieze.gradlecodegen" version "0.5.5"
//}

apply plugin: 'idea'
apply plugin: 'project-report'


description = 'The overall project that manages all artefacts of the processmanager'

ext {
    androidTarget = 27
    tomcatVersion = '7.0.68'
    androidCompatVersion = androidTarget == 25 ? '25.3.1' : androidTarget == 26 ? '26.1.0' : '27.1.1'
    dbcpSpec = "com.zaxxer:HikariCP:2.4.5"
    collectDir = "${buildDir}/artifacts"
    easywsdlver = '2.6.0'
    requirejs_version = '2.3.5'

    myJavaVersion = JavaVersion.VERSION_1_8
}

configurations {
    tomcatWars
    tomcatClasspath
    if (androidEnabled) {
        androidApps
    }
    wsDoc
}

dependencies {
    tomcatWars project(path: ":ProcessEngine:servlet", configuration: 'archives')
    tomcatWars project(path: ":PEUserMessageHandler", configuration: 'archives')
    tomcatWars project(path: ":accountmgr", configuration: 'archives')
    tomcatWars project(path: ":darwin:jvm", configuration: 'warConfig')
    tomcatWars project(path: ":webeditor", configuration: 'archives')
    tomcatWars project(path: ":DarwinServices", configuration: 'archives')


    tomcatClasspath 'mysql:mysql-connector-java:5.1.36'
    tomcatClasspath project(':DarwinJavaApi')
    tomcatClasspath project(':JavaCommonApi:jvm')
    tomcatClasspath project(':DarwinRealm')

    if (Boolean.valueOf(androidEnabledProp)) {
        androidApps project(path: ':android-auth', configuration: 'debugRuntimeElements')
        androidApps project(path: ':PMEditor', configuration: 'debugRuntimeElements')
    }

    wsDoc project(path: "ProcessEngine:servlet", configuration: 'wsDocOutput')
    wsDoc project(path: "PEUserMessageHandler", configuration: 'wsDocOutput')
}

def soapTarget = file("wiki/SOAP")
if (soapTarget.isDirectory()) {
    task wsDoc(type: Copy) {
        group = "documentation"
        dependsOn configurations.wsDoc
        destinationDir = file("wiki")

        into('.') {
            from files({ dependsOn.findAll { dep -> dep instanceof Configuration } })
//            from files({ dependsOn.findAll { dep -> dep instanceof Configuration } })
            include '**/SOAP/*.md'
            exclude '**/*InternalEndpointImpl.md'
            rename { path ->
                def file = path.substring(path.lastIndexOf('/') + 1)
                def extPos = file.lastIndexOf('.')
                def dotPos = file.lastIndexOf('.', extPos - 1) // we want the second last dot, the last is the extension one
                if (dotPos >= 0) {
                    file.substring(dotPos + 1, extPos) + "_SOAP.md"
                } else file
            }
        }

        into('.') {
            from files({ dependsOn.findAll { dep -> dep instanceof Configuration } })
            include '**/REST/*.md'
            rename { path ->
                def file = path.substring(path.lastIndexOf('/') + 1)
                def dotPos = file.lastIndexOf('.', file.lastIndexOf('.') - 1) // we want the second last dot, the last is the extension one
                def extPos = file.lastIndexOf('.')
                if (dotPos >= 0) {
                    file.substring(dotPos + 1, extPos) + "_REST.md"
                } else file
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}

task copyTomcatWars(type: Copy) {
    group = "dist"
    dependsOn configurations.tomcatWars
    from files { dependsOn.findAll { dep -> dep instanceof Configuration } }
    into "${collectDir}/webapps/"
}

task copyTomcatClasspath(type: Copy) {
    group = "dist"
    dependsOn configurations.tomcatClasspath
    from files { dependsOn.findAll { dep -> dep instanceof Configuration } }
    into "${collectDir}/tomcatlibs/"
    exclude { file -> file.name.contains("tomcat-servlet-api") }
}

if (Boolean.valueOf(androidEnabledProp)) {
    task copyAndroid(type: Copy) {
        group = "dist"
        dependsOn configurations.androidApps
        from files { dependsOn.findAll { dep -> dep instanceof Configuration } }
        into "${collectDir}/androidApps/"
    }
}

task dist {
    group = "dist"
    dependsOn copyTomcatWars, copyTomcatClasspath
    if (Boolean.valueOf(androidEnabledProp)) {
        dependsOn copyAndroid
    }
}

task run(dependsOn: [':PE-server:tomcatRun'], type: DefaultTask) {
    group = 'application'
    description = 'Run the server in a simple configuration'
}



htmlDependencyReport {
    projects = project.allprojects
}

allprojects {
    repositories {
        mavenLocal()
        jcenter()
        maven() {
            url = "https://dl.bintray.com/kotlin/kotlin-dev"
        }
        google()
        maven() {
            url = "https://dl.bintray.com/kotlin/kotlinx.html"
        }
        maven() {
            url = "https://kotlin.bintray.com/kotlinx"
        }
        maven {
            url = 'https://dl.bintray.com/jetbrains/spek'
        }
        maven { url = "https://dl.bintray.com/kotlin/kotlin-eap" }
    }

//    if (JavaVersion.current().isJava8Compatible()) {
//        tasks.withType(Javadoc) {
//            options.addStringOption('Xdoclint:none', '-quiet')
//        }
//    }

    tasks.withType(KotlinCompile) {
        kotlinOptions.jvmTarget = '1.8'
    }
}

idea {
    project {
        languageLevel = JavaVersion.VERSION_1_8
    }
    module {
        downloadSources = true
        contentRoot = projectDir
    }
}
